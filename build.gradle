/*
 * Copyright (c) 2015-2020 Uli Bubenheimer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    ext.kotlin_version = '1.6.10'

    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:0.17.1"
    }
}

plugins {
    id("maven-publish")
    id("org.jetbrains.kotlin.multiplatform") version("$kotlin_version")
}

apply plugin: "kotlinx-atomicfu"

repositories {
    mavenLocal()
    mavenCentral()
}

kotlin {
    sourceSets {
        all {
            languageSettings {
                progressiveMode = true
                optIn("kotlinx.coroutines.ExperimentalCoroutinesApi")
            }
        }

        commonMain {
            dependencies {
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0")
                implementation("org.bubenheimer:kotlinutil:1.0-SNAPSHOT")
            }
        }
    }

    targets {
        all {
            compilations {
                all {
                    kotlinOptions {
                        verbose = true
                    }
                }

                main {
                    explicitApi()
                }
            }
        }
    }

    jvm() {
        compilations {
            all {
                kotlinOptions {
                    jvmTarget = "11"
                    freeCompilerArgs = [
                            "-Xjvm-default=all"
                    ]
                }
            }
        }
    }
}

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'
